export fn drive_lug_ring_2d(lug_count, bottom_land_r, lug_height, thickness, arc_deg_top, arc_deg_bottom) {
  top_land_r = bottom_land_r + lug_height

  arc_deg_gap = (360deg - (lug_count * arc_deg_bottom)) / lug_count
  arc_deg_rise = (arc_deg_bottom - arc_deg_top) / 2.0
  arc_deg_single = arc_deg_rise + arc_deg_top + arc_deg_rise + arc_deg_gap

  assert(360, isEqualTo = arc_deg_single * lug_count)

  drive_lug_ring_sketch = startSketchOn(XY)
    |> startProfile(at = polar(angle = 0deg, length = bottom_land_r))
    |> line(endAbsolute = polar(angle = arc_deg_rise, length = top_land_r))
    |> line(endAbsolute = polar(angle = arc_deg_rise + arc_deg_top, length = top_land_r))
    |> line(endAbsolute = polar(angle = arc_deg_rise * 2 + arc_deg_top, length = bottom_land_r))
    |> line(endAbsolute = polar(angle = arc_deg_rise * 2 + arc_deg_top + arc_deg_gap, length = bottom_land_r))
    |> patternCircular2d(
         instances = lug_count,
         center = [0, 0],
         arcDegrees = 360deg,
         rotateDuplicates = true,
       )
    |> close()
    
  return drive_lug_ring_sketch
}

export fn drive_lug_ring_filled(lug_count, bottom_land_r, lug_height, thickness, arc_deg_top, arc_deg_bottom) {
  drive_lug_ring_outer_sketch = drive_lug_ring_2d(
    lug_count,
    bottom_land_r,
    lug_height,
    thickness,
    arc_deg_top,
    arc_deg_bottom)

  drive_lug_ring_outer_solid = drive_lug_ring_outer_sketch 
    |> extrude(length = thickness)
    
  return drive_lug_ring_outer_solid
}

export fn drive_lug_ring_cutout(lug_count, bottom_land_r, lug_height, cutout_buffer, thickness, arc_deg_top, arc_deg_bottom) {
  drive_lug_ring_outer_solid = drive_lug_ring_filled(
    lug_count,
    bottom_land_r,
    lug_height,
    thickness,
    arc_deg_top,
    arc_deg_bottom)

  hole_cutout = startSketchOn(XY)
  |> circle(center = [0, 0], diameter = (bottom_land_r - cutout_buffer)*2)
  |> extrude(length = thickness)

  drive_lug_ring_outer_cutout_solid = drive_lug_ring_outer_solid
  |> subtract(tools = [hole_cutout])

  return drive_lug_ring_outer_cutout_solid
}