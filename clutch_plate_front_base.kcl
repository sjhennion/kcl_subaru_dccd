// Front Clutch Plate Base


export fn clutch_plate_front_base_filled(base_plate_r, thickness, radial_oil_groove_count, outer_oil_groove_r, inner_oil_groove_r, oil_groove_trim_per) {
  oil_groove_angle = 360deg / (radial_oil_groove_count * 2) * oil_groove_trim_per

  oil_groove_single = startSketchOn(XY)
    |> startProfile(at = [0, -outer_oil_groove_r])
    |> tangentialArc(angle = oil_groove_angle, radius = outer_oil_groove_r)
    |> tangentialArc(angle = oil_groove_angle, radius = outer_oil_groove_r)
    |> tangentialArc(angle = 180deg, radius = (outer_oil_groove_r - inner_oil_groove_r) / 2)
    |> tangentialArc(angle = -oil_groove_angle, radius = inner_oil_groove_r)
    |> tangentialArc(angle = -oil_groove_angle, radius = inner_oil_groove_r)
    |> tangentialArc(angle = 180deg, radius = (outer_oil_groove_r - inner_oil_groove_r) / 2)
    |> close()

  oil_grooves_pattern = oil_groove_single
    |> patternCircular2d(instances = radial_oil_groove_count, center = [0, 0], rotateDuplicates = true)
    |> extrude(length = thickness)

  ring = startSketchOn(XY)
    |> circle(center = [0, 0], diameter = base_plate_r * 2)
    |> extrude(length = thickness)

  clutch_plate_front_base_solid = subtract(ring, tools = [oil_grooves_pattern])

  return clutch_plate_front_base_solid
}

export fn clutch_plate_front_base_cutout(base_plate_r, thickness, radial_oil_groove_count, outer_oil_groove_r, inner_oil_groove_r, oil_groove_trim_per, hole_r) {
  clutch_plate_front_base = clutch_plate_front_base_filled(
    base_plate_r,
    thickness,
    radial_oil_groove_count,
    outer_oil_groove_r,
    inner_oil_groove_r,
    oil_groove_trim_per,
  )

  hole_cutout = startSketchOn(XY) 
  |> circle(center = [0, 0], diameter = hole_r * 2)
  |> extrude(length = thickness)

  clutch_plate_front_base_cutout_solid = clutch_plate_front_base
  |> subtract(tools = [hole_cutout])

  return clutch_plate_front_base_cutout_solid
}
